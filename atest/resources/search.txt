*** Settings ***
Resource        rfdoc.txt

*** Keywords ***
User searches with "${search term}"
    Input text  search_term  ${search term}
    Click Button  search_button

User searches with "${search term}" without documentation
    Unselect Checkbox  include_doc
    User searches with "${search term}"

User searches case sensitively with "${search term}"
    Unselect Checkbox  case_insensitive
    User searches with "${search term}"

User searches case sensitively with "${search term}" without documentation
    Unselect Checkbox  case_insensitive
    User searches with "${search term}" without documentation

"${keywords}" keywords are displayed
    @{keywords} =  Get names  ${keywords}
    :FOR  ${name}  IN  @{keywords}
    \  Search Result Should Contain ${name}

Search Result Should Contain ${name}
    Page should contain element  //td/a[text()='${name}']

Then search results contain keyword "Get Title" with link to "SeleniumLibrary" and with documentation
    Page Should Contain Element  //td/a[@href='/lib/SeleniumLibrary#GetTitle' and text()='Get Title']
    Page Should Contain Element  //td/a[text()='SeleniumLibrary']
    Page Should Contain  Returns title of current page.
