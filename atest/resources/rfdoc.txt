*** Settings ***
Library         Selenium2Library  run_on_failure=Nothing
Library         OperatingSystem
Library         ${CURDIR}/../lib/NameParser.py

*** Variables ***
${HOST}  localhost
${RFDOC_PORT}  7000
${BASE URL}  http://${HOST}:${RFDOC_PORT}
${BROWSER}  firefox
@{BUILTIN KEYWORDS}  Convert To Integer  item  Converts the given item to an integer number.  No Operation  ${EMPTY}  Does absolutely nothing.
@{EXAMPLELIBRARY VERSION 1 KEYWORDS}  First Keyword  first, second=default, *args  First KW doc  My Second Keyword  some  Second KW doc
@{EXAMPLELIBRARY VERSION 2 KEYWORDS}  First Keyword Updated  first, second=default updated, *args  Updated KW doc  My Second Keyword  some  Second KW doc  Third Keyword  third  Third KW doc

*** Keywords ***
Given no libraries exist in RFDoc
    Copy file  ${CURDIR}${/}..${/}testdata/empty.db  ${CURDIR}${/}..${/}results${/}rfdoc.db

${Some libraries} exist${s} in RFDoc
    Copy file  ${CURDIR}${/}..${/}testdata/libraries.db  ${CURDIR}${/}..${/}results${/}rfdoc.db

"Main" page is open
    Go to  ${BASE URL}

"${name}" page is open
    Go to  ${BASE URL}/${name.lower()}

"${name}" page is opened
    Location should be  ${BASE URL}/${name}/

Error "${message}" is shown
    Element should contain  xpath=//ul[@class='errorlist']  ${message}

User selects "${name}" option
    Select checkbox  ${name}

Notification "${message}" is shown
    Page should contain  ${message}

Notification contains a link to ${library}
    Page should contain link  /lib/${library}  ${library}

Navigate to library "${name}"
    Go to  ${BASE URL}/lib/${name}
    Title should be  RFDoc | ${name}

User uploads ${library}
    ${file} =  Set Variable  ${library.replace(' ', '_')}.xml
    Upload ${file}

Upload ${file}
    Choose File  file  ${CURDIR}${/}..${/}testdata${/}${file}
    Click button  Upload

RFDoc contains ${library name with possible version}
    ${name} =  Get library name  ${library name with possible version}
    Navigate to library "${name}"
    @{keywords} =  Set variable  @{${library name with possible version} KEYWORDS}
    Library should contain keywords  @{keywords}

Library should contain keywords  [Arguments]  @{keywords}
    ${source} =  Get source
    :FOR  ${name}  ${args}  ${doc}  IN  @{keywords}
    \  Source should contain keyword  ${source}  ${name}  ${args}  ${doc}

Source should contain keyword  [Arguments]  ${source}  ${name}  ${args}  ${doc}
    ${match}  ${group1}  ${group2} =  Should Match Regexp  ${source}  (?s)<td id="${name}" class="kw">${name}</td>\\s*<td class="arg">(.*?)</td>\\s*<td class="doc">(.+?)</td>
    Should be equal  ${group1}  ${args}
    Should be equal  ${group2}  ${doc}
