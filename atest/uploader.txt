*** Settings ***
Resource      resources/rfdoc.txt

*** Variables ***
${INVALID URL}              localhost:1
${SCRIPT NAME}              uploader.py
${SCRIPT PATH}              ${CURDIR}${/}..${/}tools${/}${SCRIPT NAME}
${EXAMPLE LIBRARY SOURCE}   ${TESTDATA PATH}${/}sources${/}example_lib.py
${EXAMPLE LIBRARY XML V1}   ${TESTDATA PATH}${/}xmls${/}ExampleLibrary_version_1.xml
${EXAMPLE LIBRARY XML V2}   ${TESTDATA PATH}${/}xmls${/}ExampleLibrary_version_2.xml
@{EXAMPLE LIB KEYWORDS}     My Keyword  self  Does nothing.  Your Keyword  self, arg  Takes one argument and does nothing with it.

*** Test Cases ***
With one source file given
    [Setup]  Given no libraries exist in RFDoc
    ${rc}  ${output}=  Run With -u ${BASE URL} ${EXAMPLE LIBRARY SOURCE}
    Should Print ${EXAMPLE LIBRARY SOURCE} Updated
    RFDoc contains example_lib
    Should Exit With  0

With one library XML given
    [Setup]  Given no libraries exist in RFDoc
    ${rc}  ${output}=  Run With -u ${BASE URL} ${EXAMPLE LIBRARY XML V1}
    Should Print ${EXAMPLE LIBRARY XML V1} Updated
    RFDoc contains ExampleLibrary version 1
    Should Exit With  0

With multiple different files given
    [Setup]  Given no libraries exist in RFDoc
    ${rc}  ${output}=  Run With -u ${BASE URL} ${EXAMPLE LIBRARY XML V1} ${EXAMPLE LIBRARY SOURCE}
    Should Print ${EXAMPLE LIBRARY XML V1} Updated
    Should Print ${EXAMPLE LIBRARY SOURCE} Updated
    RFDoc contains ExampleLibrary version 1
    RFDoc contains example_lib
    Should Exit With  0

With a directory given
    [Setup]  Given no libraries exist in RFDoc
    ${rc}  ${output}=  Run With -u ${BASE URL} ${TESTDATA PATH}${/}sources
    Should Print ${EXAMPLE LIBRARY SOURCE} Updated
    RFDoc contains example_lib
    Should Exit With  0

With newer version given
    [Setup]  Given ExampleLibrary version 1 exist in RFDoc
    RFDoc contains ExampleLibrary version 1
    Run Keyword And Expect Error  *  RFDoc contains ExampleLibrary version 2
    ${rc}  ${output}=  Run With -u ${BASE URL} ${EXAMPLE LIBRARY XML V2}
    Should Print ${EXAMPLE LIBRARY XML V2} Updated
    Run Keyword And Expect Error  *  RFDoc contains ExampleLibrary version 1
    RFDoc contains ExampleLibrary version 2
    Should Exit With  0

With invalid path given
    ${not_existing_library}=  Set Variable  not_exist
    ${rc}  ${output}=  Run With -u ${BASE URL} ${not_existing_library}
    Should Print Library ${not_existing_library} Not Found
    Should Exit With  1

With invalid arguments
    ${invalid_argument}=  Set Variable  --invalid
    ${rc}  ${output}=  Run With ${invalid_argument}
    Should Print Argument ${invalid_argument} Is Invalid
    Should Exit With  2

Without arguments
    ${rc}  ${output}=  Run With ${EMPTY}
    Should Print Help
    Should Exit With  1

With -h
    ${rc}  ${output}=  Run With -h
    Should Print Help
    Should Exit With  0

With --help
    ${rc}  ${output}=  Run With --help
    Should Print Help
    Should Exit With  0

With -u but without URL
    ${rc}  ${output}=  Run With -u
    Should Print Argument -u Requires Value
    Should Exit With  2

With correct no files given
    ${rc}  ${output}=  Run With -u ${BASE URL}
    Should Print Help
    Should Exit With  1

With --url but no files given
    ${rc}  ${output}=  Run With --url=${BASE URL}
    Should Print Help
    Should Exit With  1

With invalid host given
    ${rc}  ${output}=  Run With --url=${INVALID URL} ${EXAMPLE LIBRARY XML V1}
    Should Print ${INVALID URL} Is Invalid Host
    Should Exit With  1

*** Keywords ***
Run With ${arguments}
    ${rc}  ${output}=  Run And Return Rc And Output  ${SCRIPT_PATH} ${arguments}
    [Return]  ${rc}  ${output}

Should Exit With
    [Arguments]  ${expected}
    Should Be Equal As Integers  ${rc}  ${expected}

Should Print Help
    Should Contain  ${output}  This script updates the documentation at Robot Framework RFDoc server.

Should Print ${test_library} Updated
    Should Contain  ${output}  Updated documentation for '${test_library}'

Should Print Argument ${argument} Requires Value
    Should Contain  ${output}  ${SCRIPT_NAME}: error: ${argument} option requires an argument
    Should Contain  ${output}  Try '${SCRIPT_NAME} --help' for more information

Should Print Argument ${argument} Is Invalid
    Should Contain  ${output}  ${SCRIPT_NAME}: error: no such option: ${argument}
    Should Contain  ${output}  Try '${SCRIPT_NAME} --help' for more information

Should Print ${host} Is Invalid Host
    Should Contain  ${output}  ${SCRIPT_NAME}: error: connection refused to '${host}', check that the host responds and is reachable.

Should Print Library ${library} Not Found
    Should Contain  ${output}  ${SCRIPT_NAME}: error: library '${library}' not found
