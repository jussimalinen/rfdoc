*** Settings ***
Resource        ../rfdoc.txt

*** Keywords ***
"${libraries}" Are Listed On The Main Page
    @{libraries} =  Get names  ${libraries}
    Main page should contain libraries  @{libraries}

Main Page Should Contain Libraries
    [Arguments]  @{libraries}
    ${regexp} =  Set Variable  <h2>Libraries</h2>\\s+<ul class="libraries">\\s+
    :FOR  ${library}  IN  @{libraries}
    \  ${regexp} =  Set Variable  ${regexp}<li><a href="/lib/${library}">${library}</a></li>\\s+
    ${regexp} =  Set Variable  ${regexp}</ul>
    ${source} =  Get source
    Should match regexp  ${source}  ${regexp}

User Opens Library Details
    Navigate to library "BuiltIn"

User Opens Library With Version Information
    User opens library details

Library Name, Version And Documentation Are Shown
    Library name should be  BuiltIn
    Page should contain  Version: 2.1
    Page should contain  An always available standard library with often needed keywords.

User Opens Library Without Version Information
    Navigate to library "ExampleLibrary"

Library Name And Documentation Are Shown
    Library name should be  ExampleLibrary
    Page should contain  First Library Doc

Library Name Should Be
    [Arguments]  ${name}
    Element should contain  xpath=//h2  ${name}

No Version Information Is Shown
    Page should not contain  Version:

User Opens Library With Initialization Information
    Navigate to library "ExampleLibrary"

Library Initialization Is Shown
    Element should contain  importing  Importing
    Page should contain  Library can be imported with optional arguments.
    Page should contain  timeout=3.0, is_regexp=False

User Opens Library Without Initialization Information
    User opens library details

Library Initialization Is Not Shown
    Page should not contain element  importing

Keywords Are Listed With Name, Argument And Documentation Information
    Library should contain keywords  @{BUILTIN KEYWORDS}

Total Number Of Keywords In Library Is Shown
    Page should contain  Altogether 72 keywords.

List Of Keywords Linking To The Keyword Details Is Shown
    Should contain link to keyword with short documentation  Sleep  Pauses the test executed for the given time.
    Should contain link to keyword with short documentation  Fail  Fails the test immediately with the given (optional) message.

Should Contain Link To Keyword With Short Documentation
    [Arguments]  ${name}  ${short doc}
    ${source} =  Get source
    Should Contain  ${source}  <a href="#${name}" title="${short doc}">${name}</a>
    Keyword should contain anchor  ${source}  ${name}

Keyword Should Contain Anchor
    [Arguments]  ${source}  ${name}
    Should contain  ${source}  <td class="kw" id="${name}">${name}</td>

Link Exist From Keyword Documentation To Another Keyword
    ${source} =  Get source
    Should contain  ${source}  <a href="#Get%20Length" class="name">Get Length</a>
    Keyword should contain anchor  ${source}  Get Length

