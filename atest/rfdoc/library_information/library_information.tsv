*** Settings ***
Resource	../rfdoc.tsv


*** Keywords ***

When user opens main page
	Navigate to main page
	
Then information that there is no libraries in RFDoc is shown 
	Page should contain	No libraries in the system.
	
Then existing libraries are listed on the main page
	Main page should contain libraries	BuiltIn	ExampleLibrary	SeleniumLibrary
	
Main page should contain libraries
	[Arguments]	@{libraries}
	${regexp} =	Set Variable	<h2>Libraries</h2>\\s+<ul class="libraries">\\s+
	:FOR	${library}	IN	@{libraries}
		${regexp} =	Set Variable	${regexp}<li><a href="/lib/${library}">${library}</a></li>\\s+
	${regexp} =	Set Variable	${regexp}</ul>
	${source} =	Get source
	Should match regexp	${source}	${regexp}

When user opens library details
	Navigate to library	BuiltIn

When user opens library with version information
	When user opens library details

Then library name, version and documentation are shown
	Library name should be	BuiltIn
	Page should contain	Version: 2.1
	Page should contain	An always available standard library with often needed keywords.

When user opens library without version information
	Navigate to library	ExampleLibrary

Then library name and documentation are shown
	Library name should be	ExampleLibrary
	Page should contain	First Library Doc

Library name should be
	[Arguments]	${name}
	Element should contain	xpath=//h2	${name}

and no version information is shown
	Page should not contain	Version:

When user opens library with initialization information
	Navigate to library	ExampleLibrary

Then library initialization is shown
	Element should contain	importing	Importing
	Page should contain	Library can be imported with optional arguments.
	Page should contain	timeout=3.0, is_regexp=False

When user opens library without initialization information
	When user opens library details

Then library initialization is not shown
	Page should not contain element	importing
	
Then keywords are listed with name, argument and documentation information
	Library should contain keywords	@{BUILTIN KEYWORDS}

then total number of keywords in library is shown
	Page should contain	Altogether 72 keywords.

Then list of keywords linking to the keyword details is shown
	Should contain link to keyword with short documentation	Sleep	
	...	Pauses the test executed for the given time.
	Should contain link to keyword with short documentation	Fail
	...	Fails the test immediately with the given (optional) message.

Should contain link to keyword with short documentation
	[Arguments]	${name}	${short doc}
	${source} =	Get source
	Should Contain	${source}	<a href="#${name}" title="${short doc}">${name}</a>
	Keyword should contain anchor	${source}	${name}

Keyword should contain anchor
	[Arguments]	${source}	${name}
	Should contain	${source}	<td class="kw" id="${name}">${name}</td>


Then link exist from keyword documentation to another keyword
	${source} =	Get source
	Should contain	${source}	<a href="#Get%20Length" class="name">Get Length</a>
	Keyword should contain anchor	${source}	Get Length
