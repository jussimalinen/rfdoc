*** Settings ***
Suite Setup     "Upload" page is open
Test Teardown   "Upload" page is open
Force Tags      Req-Upload 3  Sprint-1
Resource        ../rfdoc.txt

*** Test Cases ***
Non XML File
    Uploading "text_file.txt" fails with message "Given file is not XML."

Wrong XML Content
    Uploading "invalid_content.xml" fails with message "Given file contains invalid XML."

Non Existing File
    Uploading "non_existing_file.xml" fails with message "The submitted file is empty."

Empty Library Name
    Uploading "empty_library_name.xml" fails with message "Given file contains invalid XML."

Missing Library Name
    Uploading "missing_library_name.xml" fails with message "Given file contains invalid XML."

Missing Library Documentation
    Uploading "missing_library_doc.xml" fails with message "Given file contains invalid XML."

Empty Keyword Name
    Uploading "empty_keyword_name.xml" fails with message "Given file contains invalid XML."

Missing Keyword Name
    Uploading "missing_keyword_name.xml" fails with message "Given file contains invalid XML."

Missing Keyword Documentation
    Uploading "missing_keyword_doc.xml" fails with message "Given file contains invalid XML."

Empty Keyword
    Uploading "empty_keyword.xml" fails with message "Given file contains invalid XML."

Empty Keywords
    Uploading "empty_keywords.xml" fails with message "Given test library contains no keywords."

Missing Keywords
    Uploading "missing_keywords.xml" fails with message "Given test library contains no keywords."

*** Keywords ***
Uploading "${file}" fails with message "${message}"
    Upload invalid${/}${file}
    An error "${message}" is shown

