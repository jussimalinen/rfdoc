*** Settings ***
Library         SeleniumLibrary  ${EMPTY}  ${EMPTY}  ${SELENIUM PORT}
Library         OperatingSystem
Library         RFDocDataBase

*** Variables ***
${RFDOC PORT}  8000
${SELENIUM PORT}  4444
${HOST}  localhost:${RFDOC PORT}
${BASE URL}  http://${HOST}
${BROWSER}  firefox
@{BUILTIN KEYWORDS}  Convert To Integer  item  Converts the given item to an integer number.  No Operation  ${EMPTY}  Does absolutely nothing.
@{EXAMPLELIBRARY VERSION 1 KEYWORDS}  First Keyword  first, second=default, *args  First KW doc  My Second Keyword  some  Second KW doc
@{EXAMPLELIBRARY VERSION 2 KEYWORDS}  First Keyword Updated  first, second=default updated, *args  Updated KW doc  My Second Keyword  some  Second KW doc  Third Keyword
...  third  Third KW doc

*** Keywords ***
Given No Libraries Exist In RFDoc
    Clear RFDoc Database

Given ${some libraries} Exist${s} In RFDoc
    Copy file  ${CURDIR}/../testdata/libraries.db  ${CURDIR}/../results/rfdoc.db

Navigate To Main Page
    Navigate to and check title  ${BASE URL}  Welcome

Navigate To Upload Page
    Navigate to and check title  ${UPLOAD URL}  Upload

${When/And} ${name} Page Is Open
    Go to  ${BASE URL}/${name}

Navigate To Library
    [Arguments]  ${name}
    Go to  ${BASE URL}/lib/${name}
    Title should be  RFDoc | ${name}

${When/And} user uploads ${library}
    ${file} =  Set Variable  ${library.replace(' ', '_')}.xml
    Upload ${file}

Upload ${file}
    Input text  file  ${CURDIR}${/}..${/}testdata${/}${file}
    Click button  Upload

Navigate To Uploaded Library
    Click link  /lib/${UPLOADED LIBRARY}
    Title should be  RFDoc | ${UPLOADED LIBRARY}

Upload Library And Check Library And Keywords Are Uploaded Correctly
    [Arguments]  ${library xml}  ${library name}  @{keywords}
    Navigate to upload page
    Upload file  ${library xml}  ${library name}
    Library upload should be successful  ${library name}
    Library should contain keywords  @{keywords}

Library Upload Should Be Successful
    [Arguments]  ${library name}
    Upload page should contain success information  ${library name}
    Navigate to uploaded library

And RFDoc contains ${library}
    ${name w/o version} =  Set Variable  ${library.split()[0]}
    Navigate to library  ${name w/o version}
    ${source} =  Get source
    @{keywords} =  Set variable  @{${library} KEYWORDS}
    :FOR  ${name}  ${args}  ${doc}  IN  @{keywords}
    \  Source should contain keyword  ${source}  ${name}  ${args}  ${doc}

Source Should Contain Keyword
    [Arguments]  ${source}  ${name}  ${args}  ${doc}
    ${match}  ${group1}  ${group2} =  Should Match Regexp  ${source}  (?s)<td class="kw" id="${name}">${name}</td>\\s*<td class="arg">(.*?)</td>\\s*<td class="doc">(.+?)</td>
    Should Be Equal  ${group1}  ${args}
    Should Be Equal  ${group2}  ${doc}

Then an error "${message}" is shown
    Element should contain  xpath=//ul[@class='errorlist']  ${message}

Then a notification "${message}" is shown
    Page should contain  ${message}

When User Selects "${name}" Option
    Select checkbox  ${name}

and the notification contains a link to ${library}
    Page Should Contain Link  /lib/${library}  ${library}
#    Successfully uploaded library <a href="/lib/BuiltIn">BuiltIn</a>.
    